include(AddLLVM) # for add_lit_testsuite
macro(pythonize_bool var)
  if (${var})
    set(${var} True)
  else()
    set(${var} False)
  endif()
endmacro()

set(_LIBUNWIND_TESTS alignment libunwind_01 libunwind_02 unw_getcontext libunwind_get_registers)
set(_LIBUNWIND_TEST_BINARIES)
foreach(_test ${_LIBUNWIND_TESTS})
  if (LIBUNWIND_ENABLE_SHARED)
    add_executable(test-${_test}-shared EXCLUDE_FROM_ALL ${_test}.pass.cpp)
    target_link_libraries(test-${_test}-shared unwind_shared)
    target_compile_features(test-${_test}-shared PUBLIC cxx_std_11)
    target_compile_options(test-${_test}-shared PUBLIC ${LIBUNWIND_CXX_FLAGS})
    list(APPEND _LIBUNWIND_TEST_BINARIES test-${_test}-shared)
  endif()
  if (LIBUNWIND_ENABLE_STATIC)
    add_executable(test-${_test}-static EXCLUDE_FROM_ALL ${_test}.pass.cpp)
    target_link_libraries(test-${_test}-static unwind_static)
    target_compile_features(test-${_test}-static PUBLIC cxx_std_11)
    target_compile_options(test-${_test}-static PUBLIC ${LIBUNWIND_CXX_FLAGS})
    list(APPEND _LIBUNWIND_TEST_BINARIES test-${_test}-static)
  endif()
endforeach()

message(STATUS "TESTS: ${_LIBUNWIND_TEST_BINARIES}")
add_custom_target(unwind-test-binaries DEPENDS ${_LIBUNWIND_TEST_BINARIES})


if (NOT DEFINED LIBCXX_ENABLE_SHARED)
  set(LIBCXX_ENABLE_SHARED ON)
endif()

pythonize_bool(LIBUNWIND_BUILD_32_BITS)
pythonize_bool(LIBCXX_ENABLE_SHARED)
pythonize_bool(LIBUNWIND_ENABLE_SHARED)
pythonize_bool(LIBUNWIND_ENABLE_THREADS)
pythonize_bool(LIBUNWIND_ENABLE_EXCEPTIONS)
pythonize_bool(LIBUNWIND_BUILD_EXTERNAL_THREAD_LIBRARY)
set(LIBUNWIND_TARGET_INFO "libcxx.test.target_info.LocalTI" CACHE STRING
    "TargetInfo to use when setting up test environment.")
set(LIBUNWIND_EXECUTOR "None" CACHE STRING
    "Executor to use when running tests.")

set(AUTO_GEN_COMMENT "## Autogenerated by libunwind configuration.\n# Do not edit!")
configure_file(
  ${CMAKE_CURRENT_SOURCE_DIR}/lit.site.cfg.in
  ${CMAKE_CURRENT_BINARY_DIR}/lit.site.cfg
  @ONLY)

add_lit_testsuite(check-unwind "Running libunwind tests"
  ${CMAKE_CURRENT_BINARY_DIR}
  DEPENDS ${LIBUNWIND_TEST_DEPS}
  )
